plugins {
    id 'java'
    id 'project-report'
    id 'jacoco'
    id 'findbugs'
    id 'checkstyle'
    id 'maven'
}

group 'com.marlowsoft'
version '1.0-SNAPSHOT'

compileJava {
    sourceCompatibility "1.8"
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:19.0',
            'com.google.inject:guice:4.0',
            'org.jsoup:jsoup:1.8.3',
            'org.jcodec:jcodec-javase:0.1.9',
            'redis.clients:jedis:2.8.1',
            'com.google.code.gson:gson:2.6.2'
    testCompile 'junit:junit:4.12',
            'org.nanohttpd:nanohttpd:2.3.0'
}

findbugs {
    effort = "max"
    reportLevel = "high"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
        html.enabled true
    }
}

javadoc {
    classpath += sourceSets.test.compileClasspath
    source += sourceSets.test.allJava
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// these next two tasks were blatantly copypasted from:
//  http://ianp.org/2011/11/04/installing-to-the-local-maven-repo-with-gradle/
task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
